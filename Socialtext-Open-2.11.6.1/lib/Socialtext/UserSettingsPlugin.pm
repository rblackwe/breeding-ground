#
# Copyright (C) 2004-2006 by Socialtext, Inc.
#
package Socialtext::UserSettingsPlugin;
use strict;
use warnings;

use base 'Socialtext::Plugin';

use Class::Field qw( const field );
use Email::Address;
use Email::Valid;
use Socialtext::AppConfig;
use Socialtext::EmailSender;
use Socialtext::Permission qw( ST_ADMIN_WORKSPACE_PERM );
use Socialtext::TT2::Renderer;
use Socialtext::User;
use Socialtext::WorkspaceInvitation;
use Template::Iterator::AlzaboWrapperCursor;
use Socialtext::URI;

sub class_id {'user_settings'}
const cgi_class => 'Socialtext::UserSettings::CGI';
field 'users_new_ids';
field users_already_present => [];

sub register {
    my $self = shift;
    my $registry = shift;
    $registry->add( action => 'settings' );
    $registry->add( action => 'users_settings' );
    $registry->add( action => 'users_listall' );
    $registry->add( action => 'users_invitation' );
    $registry->add( action => 'users_invite' );
    $registry->add( action => 'users_search' );
}

# for backwards compat
sub settings {
    my $self = shift;
    $self->redirect('action=users_settings');
}

sub users_settings {
    my $self = shift;
    if ( $self->hub()->current_user()->is_guest() ) {
        my $app = Socialtext::WebApp->NewForNLW;
        Socialtext::Challenger->Challenge(
            type => 'settings_requires_account' );
    }

    $self->_update_current_user()
        if $self->cgi->Button;

    my $settings_section = $self->template_process(
        'element/settings/users_settings_section',
        user => $self->hub->current_user,
        $self->status_messages_for_template,
    );

    $self->screen_template('view/settings');
    return $self->render_screen(
        settings_table_id => 'settings-table',
        settings_section  => $settings_section,
        hub               => $self->hub,
        display_title     => 'Users: My Settings',
        pref_list         => $self->_get_pref_list,
    );
}

sub _update_current_user {
    my $self = shift;
    my $user = $self->hub->current_user;

    my %update;
    if (   $self->cgi->old_password
        or $self->cgi->new_password
        or $self->cgi->new_password_retype ) {
        $self->add_error('Old password is incorrect')
            unless $user->password_is_correct( $self->cgi->old_password );
        $self->add_error('New passwords do not match')
            unless $self->cgi->new_password eq
            $self->cgi->new_password_retype;

        return if $self->input_errors_found;

        $update{password} = $self->cgi->new_password;
    }

    $update{first_name} = $self->cgi->first_name;
    $update{last_name}  = $self->cgi->last_name;

    eval { $user->update_store(%update) };
    if ( my $e
        = Exception::Class->caught('Socialtext::Exception::DataValidation') )
    {
        $self->add_error($_) for $e->messages;
    }
    elsif ($@) {
        die $@;
    }

    return if $self->input_errors_found;

    $self->message('Changes Saved');
}

sub get_admins {
    my $self = shift;
    my $workspace = $self->hub->current_workspace;
    my @admins;
    my $users_with_roles = $workspace->users_with_roles();

    while ( my ( $user, $role ) = $users_with_roles->next ) {
        if ( $role->name eq 'workspace_admin' ) {
            push( @admins, $user->email_address );
        }
    }
    return (@admins);
}

sub users_listall {
    my $self = shift;
    if ( $self->hub()->current_user()->is_guest() ) {
        Socialtext::Challenger->Challenge(
            type => 'settings_requires_account' );
    }

    $self->_update_users_in_workspace()
        if $self->cgi->Button;

    my $settings_section = $self->template_process(
        'element/settings/users_listall_section',
        users_with_roles => Template::Iterator::AlzaboWrapperCursor->new(
            $self->hub->current_workspace->users_with_roles
        ),
        $self->status_messages_for_template,
    );

    my $display_title =
        $self->hub->checker->check_permission('admin_workspace')
        ? 'Users: Manage All Users'
        : 'Users: List All Users';

    $self->screen_template('view/settings');
    return $self->render_screen(
        settings_table_id => 'settings-table',
        settings_section  => $settings_section,
        hub               => $self->hub,
        display_title     => $display_title,
        pref_list         => $self->_get_pref_list,
    );
}

sub _update_users_in_workspace {
    my $self = shift;
    $self->hub->assert_current_user_is_admin;


    my $ws = $self->hub->current_workspace;
    my %removed;
    for my $user_id ( $self->cgi->remove_user ) {
        my $user = Socialtext::User->new( user_id => $user_id );

        $ws->remove_user( user => $user );

        $removed{$user_id} = 1;
    }

    for my $user_id ( grep { !$removed{$_} } $self->cgi->reset_password ) {
        my $user = Socialtext::User->new( user_id => $user_id );

        $user->set_confirmation_info( is_password_change => 1 );
        $user->send_password_change_email();
    }

    my %should_be_admin = map { $_ => 1 } $self->cgi->should_be_admin;
    if ( keys %should_be_admin ) {

        my $users_with_roles
            = $self->hub->current_workspace->users_with_roles;

        while ( my $tuple = $users_with_roles->next ) {

            my $user = $tuple->[0];
            my $role = $tuple->[1];

            # REVIEW - this is a hack to prevent us from removing the
            # impersonator role from users in this loop. The real
            # solution is to replace the is/is not admin check with
            # something like a pull down or radio group which allows
            # for assigning of different roles.
            next
                if $role->name ne 'workspace_admin'
                and $role->name ne 'member';

            next
                if $should_be_admin{ $user->user_id() }
                and $ws->user_has_permission(
                user       => $user,
                permission => ST_ADMIN_WORKSPACE_PERM,
                );

            next
                if not $should_be_admin{ $user->user_id() }
                and not $ws->user_has_permission(
                user       => $user,
                permission => ST_ADMIN_WORKSPACE_PERM,
                );

            my $is_selected = $user->workspace_is_selected(
                workspace => $self->hub()->current_workspace() );

            if ( $should_be_admin{ $user->user_id } ) {
                $ws->assign_role_to_user(
                    user        => $user,
                    role        => Socialtext::Role->WorkspaceAdmin(),
                    is_selected => $is_selected,
                );
            }
            else {
                $ws->assign_role_to_user(
                    user        => $user,
                    role        => Socialtext::Role->Member(),
                    is_selected => $is_selected,
                );
            }
        }
    }
    else {
        $self->add_warning("Can't remove privileges of the last admin");
    }

    $self->message('Changes Saved');

    return;
}

# XXX this method doesn't seem to have test coverage
sub users_invitation {
    my $self = shift;
    if ( !$self->hub->checker->check_permission('request_invite') ) {
        $self->hub->assert_current_user_is_admin;
    }

    my $ws = $self->hub->current_workspace;
    my $restrict_invitation_to_search = $ws->restrict_invitation_to_search;
    my $invitation_filter = $ws->invitation_filter;
    my $template_dir = $ws->invitation_template;

    my $template =
      $restrict_invitation_to_search
      ? 'element/settings/users_invite_search_section'
      : 'element/settings/users_invite_section';

    $self->hub->action(
        $restrict_invitation_to_search ? 'users_search' : 'users_invite' );


    my $settings_section = $self->template_process(
        $template,
        users_with_roles => Template::Iterator::AlzaboWrapperCursor->new(
            $self->hub->current_workspace->users_with_roles
        ),
        invitation_filter             => $invitation_filter,
        workspace_invitation_body     => "email/$template_dir/workspace-invitation-body.html",
        $self->status_messages_for_template,
    );

    $self->screen_template('view/settings');
    return $self->render_screen(
        settings_table_id => 'settings-table',
        settings_section  => $settings_section,
        hub               => $self->hub,
        display_title     => 'Users: Invite New Users',
        pref_list         => $self->_get_pref_list,
    );
}

sub users_invite {
    my $self = shift;
    if ( !$self->hub->checker->check_permission('request_invite') ) {
        $self->hub->assert_current_user_is_admin;
    }

    if ( $self->cgi->users_new_ids ) {
        my @lines = $self->_split_email_addresses( $self->cgi->users_new_ids );

        unless (@lines) {
            $self->add_error("No email addresses specified");
            return;
        }

        my @emails;
        my @invalid;
        for my $line (@lines) {
            my ( $email, $first_name, $last_name )
              = $self->_parse_email_address($line);
            unless ($email) {
                push @invalid, $line;
                next;
            }

            push @emails, $email;
        }

        my $html = $self->_invite_users(\@emails, \@invalid);
        return $html if $html;
    }
}

sub users_search {
    my $self = shift;
    my $filter = $self->hub->current_workspace->invitation_filter();
    my $template_dir = $self->hub->current_workspace->invitation_template();
    if ( !$self->hub->checker->check_permission('request_invite') ) {
        $self->hub->assert_current_user_is_admin;
    }

    my @users;

    if ( $self->cgi->Button ) {
        if ( $self->cgi->Button eq 'Invite' && $self->cgi->email_addresses ) {
            my @emails = $self->cgi->email_addresses;
            my @invalid;
            my $html = $self->_invite_users(\@emails, \@invalid);
            return $html if $html;
        }
    }

    if ( $self->cgi->user_search ) {
        @users = Socialtext::User->Search( $self->cgi->user_search );
    }

    if (@users && $filter) {
        @users = grep { $_ if ($_->{email_address} =~ qr/$filter/) } @users;
    }

    my $settings_section = $self->template_process(
        'element/settings/users_invite_search_section',
        invitation_filter => $filter,
        users_with_roles => Template::Iterator::AlzaboWrapperCursor->new(
            $self->hub->current_workspace->users_with_roles
        ),
        $self->status_messages_for_template,
        workspace_invitation_body     => "email/$template_dir/workspace-invitation-body.html",
        users => \@users,
        search_performed => 1,
    );

    $self->screen_template('view/settings');
    return $self->render_screen(
        settings_table_id => 'settings-table',
        settings_section  => $settings_section,
        hub               => $self->hub,
        display_title     => 'Users: Invite New Users',
        pref_list         => $self->_get_pref_list,
    );
}

sub _invite_users {
    my $self = shift;
    my ($emails, $invalid) = @_;
    my $filter = $self->hub->current_workspace->invitation_filter();

    my %users;
    my @present;
    for my $email (@{ $emails }) {
        if ($filter) {
            unless ( $email =~ qr/$filter/ ) {
                push @{ $invalid }, $email;
                next;
            }
        }
        next if $users{$email};

        my $user = Socialtext::User->new( email_address => $email );
        if (   $user
            && $self->hub->current_workspace->has_user( user => $user ) )
        {
            push @present, $email;
            next;
        }

        $users{$email} = {
            username      => $email,
            email_address => $email,
        };
    }

    my $extra_invite_text = $self->cgi->append_invitation ?
                            $self->cgi->invitation_text :
                            '';

    if ( $self->hub->checker->check_permission('admin_workspace') ) {
        for my $user_data ( values %users ) {
            $self->invite_one_user( $user_data, $extra_invite_text );
        }
    }
    else {
        $self->invite_request_to_admin( \%users, $extra_invite_text );
    }

    my $settings_section = $self->template_process(
        'element/settings/users_invited_section',
        users_invited         => [ sort keys %users ],
        users_already_present => [ sort @present ],
        invalid_addresses     => [ sort @{ $invalid } ],
        $self->status_messages_for_template,
    );

    $self->screen_template('view/settings');
    return $self->render_screen(
        settings_table_id => 'settings-table',
        settings_section  => $settings_section,
        hub               => $self->hub,
        display_title     => 'Users: Invite New Users',
        pref_list         => $self->_get_pref_list,
    );
}

sub _split_email_addresses {
    my $self = shift;
    return grep /\S/, map {
        s/^\s*(.*?)\s*$/$1/;
        $_;
    } split /[,\r\n]+/, $_[0];
}

sub _parse_email_address {
    my $self = shift;
    my $email = shift;

    return unless defined $email;

    $email =~ s/^mailto://;
    $email =~ s/^<(.+)>$/$1/;

    return unless Email::Valid->address($email);

    my ($address) = Email::Address->parse($email);

    return unless $address;

    my ( $first, $last );
    if ( grep { defined && length } $address->name ) {
        my $name = $address->name;
        $name =~ s/^\s+|\s+$//g;

        ( $first, $last ) = split /\s+/, $name, 2;
    }

    return lc $address->address, $first, $last;
}

sub invite_request_to_admin {
    my $self = shift;
    my $user_hash   = shift;
    my $extra_text  = shift;
    my $user_string = '';
    my $admin_email = join( ',', $self->get_admins );
    my @invited_users;
    foreach my $user ( values %$user_hash ) {
        push( @invited_users, $user->{email_address} );
    }

    my $renderer = Socialtext::TT2::Renderer->instance();
    my $subject  = 'Request to invite new users to the '
        . $self->hub->current_workspace->title
        . ' workspace';

    my $template_dir = $self->hub->current_workspace->invitation_template;
    my $url = $self->hub->current_workspace->uri . "?action=users_invite";

    my %vars = (
        inviting_user   => $self->hub->current_user->email_address,
        workspace_title => $self->hub->current_workspace->title,
        invited_users   => [@invited_users],
        url             => $url,
        extra_text      => $extra_text,
        appconfig       => Socialtext::AppConfig->instance,
    );

    my $text_body = $renderer->render(
        template => "email/$template_dir/invite-request-email.txt",
        vars     => \%vars,
    );

    my $html_body = $renderer->render(
        template => "email/$template_dir/invite-request-email.html",
        vars     => \%vars,
    );

    Socialtext::EmailSender->send(
        from      => $self->hub->current_user->name_and_email,
        to        => $admin_email,
        subject   => $subject,
        text_body => $text_body,
        html_body => $html_body,
    );

}

sub invite_one_user {
    my $self = shift;
    my $user_data  = shift;
    my $extra_text = shift;

    my $invitation =
      Socialtext::WorkspaceInvitation->new( workspace => $self->hub->current_workspace,
                                            from_user => $self->hub->current_user,
                                            invitee   => $user_data->{email_address},
                                            extra_text => $extra_text,
                                            viewer => $self->hub->viewer );
    $invitation->send();
}

package Socialtext::UserSettings::CGI;

use base 'Socialtext::CGI';
use Socialtext::CGI qw( cgi );

cgi 'Button';
cgi new_password        => '-trim';
cgi new_password_retype => '-trim';
cgi old_password        => '-trim';
cgi 'remove_user';
cgi 'reset_password';
cgi 'should_be_admin';
cgi 'user_search';
cgi 'email_addresses';
cgi 'users_new_ids';
cgi 'first_name';
cgi 'last_name';
cgi 'append_invitation';
cgi 'invitation_text';

1;

